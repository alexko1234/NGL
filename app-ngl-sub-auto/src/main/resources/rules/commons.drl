#created on: Jul 7, 2014
package birds

import java.util.Date;
import fr.genoscope.lis.devsi.birds.api.jobscheduler.JobScheduler;
import fr.cea.ig.auto.submission.SubmissionServices;
import fr.cea.ig.auto.submission.SubmissionServicesFactory;
import fr.cea.ig.auto.submission.ISubmissionServices;
import fr.genoscope.lis.devsi.birds.api.entity.JobProperties;



rule "Syntax order command line for sendXML specification"
	@BirdsRule(unixSyntaxRule)
	dialect 'java'
	salience 200
	when
		$strategy : UnixCommandSyntaxStrategy ( name == "sendXMLSyntax" , commandLine == null)
	then
		Logger log = Logger.getLogger("ngl.sub.rules");
		log.debug("Syntax sendXML : curl https://www.ebi.ac.uk/ena/submit/drop-box/submit/?auth=ERA%20era-drop-9%20N7mo%2B8F4aHH%2BrCjLTuMo59xwfFo%3D -k -F \"SAMPLE=@sampleXml\" -F \"RUN=@runXml\" -F \"SUBMISSION=@submissionXML\" -F \"STUDY=@studyXML\" -F \"EXPERIMENT=@experimentXML\" ");
		String parameterLine = $strategy.getParameterValue().get("optK")+" ";
		for (String key : $strategy.getParameterValue().keySet()) {
			if(!key.equals("optK") && !key.equals("listAC") && !$strategy.getParameterValue().get(key).equals("-F null") && !key.equals("OptionCD"))
	         parameterLine += $strategy.getParameterValue().get(key) + " ";
	    }
		//Add redirect to AC file
		$strategy.setCommandLine($strategy.getParameterValue().get("OptionCD")+";"+$strategy.getExecutableName() + " " + parameterLine +" > "+$strategy.getParameterValue().get("listAC"));
		log.debug("command line : " + $strategy.getCommandLine());
		
		modify($strategy) {}
end


rule "Syntax order command line for createXML specification"
	@BirdsRule(unixSyntaxRule)
	dialect 'java'
	salience 200
    when
        $strategy : UnixCommandSyntaxStrategy ( name == "createXMLSyntax" , commandLine == null)
    then
        Logger log = Logger.getLogger("ngl.sub.rules");
		$strategy.setCommandLine($strategy.getExecutableName());
		log.debug("command line : " + $strategy.getCommandLine());

end


#init property retryJob
rule "Init property retry Job "
@BirdsRule( ExecutionService )
	dialect 'java'
	salience 500
	no-loop
	when
		$executionService : ExecutionService( status == ExecutionService.PRE_JOB_EXECUTION_STATUS)
		$job : Job( treatmentSpecification.specificationGroup == false,  treatmentSpecification.name != null , treatmentSpecification.name in ("sendXML", "transfertRawData", "sendReleaseXML"))
		not ($jobProperties : JobProperties(key=="retryJob") from $job.jobProperties)
	then
		$job.setProperty("retryJob","1");
end

#retry 3 times when job error
rule "Retry Job "
@BirdsRule( ExecutionService )
	dialect 'java'
	salience 400
	no-loop
	when
		$executionService : ExecutionService( status == ExecutionService.POST_JOB_EXECUTION_STATUS)
		$job : Job( treatmentSpecification.specificationGroup == false, executionState == Job.ERROR_STATUS,  treatmentSpecification.name != null , treatmentSpecification.name in ("sendXML", "transfertRawData", "sendReleaseXML"))
		not ($jobProperties : JobProperties(key=="retryJob", value=="3") from $job.jobProperties)
	then
		int nb = Integer.parseInt($job.getProperty("retryJob")) +1;
		$job.setProperty("retryJob",""+nb);
		$job.setExecutionState(Job.NEW_STATUS);		
end

#Send mail when job is exited
rule "Send error mail when job exited"
@BirdsRule( ExecutionService )
	dialect 'java'
	salience 100
	no-loop
	when
		$executionService : ExecutionService( status == ExecutionService.POST_JOB_EXECUTION_STATUS)
		$job : Job( treatmentSpecification.specificationGroup == false, executionState == Job.ERROR_STATUS )
	then
		if($job.getProperty("retryJob")==null || ($job.getProperty("retryJob")!=null && $job.getProperty("retryJob").equals("3"))){
	
			//Send mail
			String errorMessage = "Error during executing job. Check outputFile and log \n"+
			"STDOUT "+$job.getProperty(Job.STDOUT)+"\n"+
			"STDERR "+$job.getProperty(Job.STDERR)+"\n"+
			"SCHEDULER STDOUT "+$job.getProperty(JobScheduler.JOB_SCHEDULER_STD_OUTPUT_PATH+JobScheduler.JOB_SCHEDULER_STD_OUTPUT_FILE)+"\n"+
			"SCHEDULER STDERR "+$job.getProperty(JobScheduler.JOB_SCHEDULER_STD_ERROR_PATH+JobScheduler.JOB_SCHEDULER_STD_ERROR_FILE)+"\n"+
			"Exception "+$job.getProperty("Exception");
		
			SubmissionServicesFactory.getInstance().sendMail("william","JOB ERROR "+$job.getId(),errorMessage);
		}
		
end

function void updateParameterXML(String valueParam, Job job, String resourceName)
{
	if(job.getUniqueJobResource(resourceName).getProperty(valueParam)!=null)
		job.setParameterValue(valueParam,job.getParameterValue(valueParam).getValue().replace("%"+valueParam+"%",job.getUniqueJobResource(resourceName).getProperty(valueParam)));
	else
		job.setParameterValue(valueParam,null);
}