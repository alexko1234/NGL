#created on: Jul 7, 2014
package birds

rule "Define variable lineParmeter for sendXML specification"
	@BirdsRule(fsRule)
	dialect 'java'
	salience 300
	no-loop
    when
       $treatSpec : TreatmentSpecification(name=="sendXML", project.name=="SRA")
       $job : Job()
    then
       Logger log = Logger.getLogger("ngl.sub.rules");
       log.debug("Set parameter for spec sendXML");
       updateParameterXML("xmlSubmission",$job);
       updateParameterXML("xmlStudys",$job);
       updateParameterXML("xmlSamples",$job);
       updateParameterXML("xmlExperiments",$job);
       updateParameterXML("xmlRuns",$job);
       $job.setParameterValue("listAC",$job.getParameterValue("listAC").getValue().replace("%submissionDate%",$job.getUniqueJobResource("subToSend").getProperty("submissionDate")));
	
end

rule "Syntax order command line for sendXML specification"
	@BirdsRule(unixSyntaxRule)
	dialect 'java'
	salience 200
	when
		$strategy : UnixCommandSyntaxStrategy ( name == "sendXMLSyntax" , commandLine == null)
	then
		Logger log = Logger.getLogger("ngl.sub.rules");
		log.debug("Syntax sendXML : curl https://www.ebi.ac.uk/ena/submit/drop-box/submit/?auth=ERA%20era-drop-9%20N7mo%2B8F4aHH%2BrCjLTuMo59xwfFo%3D -k -F \"SAMPLE=@sampleXml\" -F \"RUN=@runXml\" -F \"SUBMISSION=@submissionXML\" -F \"STUDY=@studyXML\" -F \"EXPERIMENT=@experimentXML\" ");
		String parameterLine = $strategy.getParameterValue().get("optK")+" ";
		for (String key : $strategy.getParameterValue().keySet()) {
			if(!key.equals("optK") && !key.equals("listAC") && !$strategy.getParameterValue().get(key).equals("-F null"))
	         parameterLine += $strategy.getParameterValue().get(key) + " ";
	    }
		//Add redirect to AC file
		$strategy.setCommandLine($strategy.getExecutableName() + " " + parameterLine +" > "+$strategy.getParameterValue().get("listAC"));
		log.debug("command line : " + $strategy.getCommandLine());
		
		modify($strategy) {}
end

rule "POST DONE EXECUTION of sendXML : File AC results operation"
	@BirdsRule( ExecutionService )
	dialect 'java'
	salience 200
	when
		$executionService : ExecutionService( status == ExecutionService.POST_JOB_EXECUTION_STATUS)
		$job : Job( treatmentSpecification!=null, treatmentSpecification.name == "sendXML", treatmentSpecification.project.name == "SRA", executionState == Job.DONE_STATUS )
	then
		Logger log = Logger.getLogger("ngl.sub.rules");
		//Call service treatment AC files results 
		JobResource jobResource = $job.getUniqueJobResource("subToSend");
		String codeSubmission = jobResource.getProperty("code");
		String pathAC = jobResource.getProperty("submissionDirectory")+File.separator+$job.getParameterValue("listAC").getValue();
		File fileAC = new File(pathAC);
		log.debug("path AC="+pathAC);
		JSONDevice jsonDevice = new JSONDevice();
		String url = ProjectProperties.getProperty("server")+"/submissions/"+codeSubmission+"/ebiAc";
		String jsonResult = jsonDevice.httpPut(ProjectProperties.getProperty("server")+"/submissions/"+codeSubmission+"/ebiAc", fileAC);
		log.debug("Result "+jsonResult);
		Set<ResourceProperties> resourcePropertiesSet = jsonDevice.parseJSONFromString(jsonResult);
		log.debug("Resource Properties "+resourcePropertiesSet);
		//if submission.state=ERROR_SendXML set job status to JOB EXITED
		//Get unique resource 
		String state = resourcePropertiesSet.iterator().next().get("state.code");
		log.debug("State from treatment AC "+state);
		if(state.equals("errorResultSendXml"))
			$job.setExecutionState(Job.ERROR_STATUS);
end

#create function to replace value in parameter with evaluate null value

function void updateParameterXML(String valueParam, Job job)
{
	if(job.getUniqueJobResource("subToSend").getProperty(valueParam)!=null)
		job.setParameterValue(valueParam,job.getParameterValue(valueParam).getValue().replace("%"+valueParam+"%",job.getUniqueJobResource("subToSend").getProperty(valueParam)));
	else
		job.setParameterValue(valueParam,null);
}